plugins {
    id "se.inera.intyg.plugin.common" version "3.2.9-SNAPSHOT" apply false
    id "io.spring.dependency-management" version "1.1.0"
    id 'idea'
}

ext {
    funktionstjansterGrpSchemasVersion = "1.0.6"

    rivtaDirectoryOrganizationSchemasVersion = "1.1.RC5.6"
    rivtaStoreLogSchemasVersion = "2.0.1"
    rivtaHsaAuthorizationmanagementSchemasVersion = "1.0.RC4.6"
    rivtaHsaEmployeeSchemasVersion = "1.0.RC4.6"
    rivtaGetPersonsPersonSchemasVersion = "3.1.1"

    intygClinicalprocessSchemasVersion = "1.0.24"

    srsSchemasVersion = "0.0.16"

    schemasContractVersion = "2.1.8"

    commonsIoVersion = "2.11.0"
    commonsCollectionsVersion = "4.4"
    cxfVersion = "3.6.1"
    embeddedRedisVersion = "0.7.2"
    guavaVersion = "31.1-jre"
    jakartaJwsApiVersion = "3.0.0"
    jaxb2Version = "0.13.1"
    jaxb2NamespacePrefixVersion = "2.0"
    jaxbImplVersion = "2.3.6"
    saxonVersion = "11.4"
    shedlockSpringVersion = "4.42.0"
    springSecuritySaml2CoreVersion = "1.0.3.RELEASE" //Higher versions require opensaml 2.6.6 (not available 2020-03-23)
    uapVersion = "1.5.3"
    xmlSecVersion = "3.0.1"

    springbootMavenBomVersion = "2.7.13"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    test.dependsOn('createVersionPropertyFile')
    compileTestJava.dependsOn('createVersionPropertyFile')

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    dependencies {
        implementation "javax.annotation:javax.annotation-api"
        implementation "jakarta.xml.bind:jakarta.xml.bind-api"
        implementation "jakarta.xml.ws:jakarta.xml.ws-api"
        implementation "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation "org.glassfish.jaxb:jaxb-runtime"
        testImplementation "com.sun.xml.messaging.saaj:saaj-impl"

        testImplementation "junit:junit"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.springframework:spring-test"
        testImplementation "org.mockito:mockito-core"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    }

    group = 'se.inera.intyg.infra'
    version = System.properties['buildVersion'] ?: '3.19.1-SNAPSHOT'

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.register('createVersionPropertyFile', VersionPropertyFileTask)

    tasks.register('install') {
        description("Publishes artifact to local maven repository.")
        dependsOn(publishToMavenLocal)
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories.maven {
            url = uri("https://nexus.drift.inera.se/repository/maven-releases/")
            credentials {
                username = System.properties['ineraNexusUsername']
                password = System.properties['ineraNexusPassword']
            }
        }
    }
}

clean {
    setDelete(fileTree(buildDir) {
        exclude("/tmp/.cache/expanded/expanded.lock")
    })
}

tasks.register('tagRelease', TagReleaseTask)
