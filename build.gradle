plugins {
  id 'org.ajoberstar.grgit' version '1.5.1'
}

allprojects  {
  apply plugin: 'maven'
  apply plugin: 'org.ajoberstar.grgit'

  def baseVersion = '2.6.'

  logger.quiet("Version: " + System.env.BUILD_NUMBER)
  //logger.quiet("P Version: " + project.BUILD_NUMBER)

  group = 'se.inera.intyg.common'
  version = System.getProperty('BUILD_NUMBER') ? baseVersion + System.getProperty('BUILD_NUMBER') : '0-SNAPSHOT'

  repositories {
    mavenLocal()
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/thirdparty/" }
    maven { url "http://repo.maven.apache.org/maven2" }
  }

}

subprojects {
  apply plugin: 'java'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  task createVersionPropertyFile(dependsOn: processResources) << {
    def propertyFile = "${buildDir}/resources/main/version.properties"
    ant.touch(file: propertyFile, mkdirs: "true")
    ant.propertyfile(file: propertyFile) {
      entry(key: 'project.version', default: rootProject.version)
      entry(key: 'buildTime', default: new Date())
      entry(key: 'gitCommit', default: "${grgit.head().id}")
      entry(key: 'gitBranch', default: "${grgit.branch.getCurrent().getName()}")
    }
  }

  jar.dependsOn(createVersionPropertyFile)
}

task tagRelease {
  description = 'Tags the current head with the projects version.'
  doLast {
    grgit.tag.add {
      name = version
      message = "Release of ${version}"
    }
    grgit.push(tags: true)
  }
}

task printInfo {
  //logger.quiet("Version: " + System.getProperty('BUILD_NUMBER'))
  System.properties.each { logger.quiet(it.key + ": " + it.value) }
  doLast {
    System.properties.each { logger.quiet(it.key + ": " + it.value) }
    // logger.quiet("Version in dolast: " + System.getProperty('BUILD_NUMBER'))
  }
}
