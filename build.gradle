plugins {
    id "se.inera.intyg.plugin.common" version "3.2.10-SNAPSHOT" apply false
    id "io.spring.dependency-management" version "1.1.3" apply false
    id 'idea'
}

ext {
    // Below three schema versions can be removed when old HSA package is retired.
    rivtaDirectoryOrganizationSchemasVersion = "1.1.RC5.6"
    rivtaHsaAuthorizationmanagementSchemasVersion = "1.0.RC4.6"
    rivtaHsaEmployeeSchemasVersion  = "1.0.RC4.6"
    // Above three schema versions can be removed when old HSA package is retired.

    funktionstjansterGrpSchemasVersion = "1.0.6.1"
    intygClinicalprocessSchemasVersion = "1.0.24.1"
    rivtaGetPersonsPersonSchemasVersion = "3.1.1.1"
    rivtaStoreLogSchemasVersion = "2.0.1.1"
    schemasContractVersion = "2.1.8.1"
    schemasSupportVersion = "2.1.5.1"
    srsSchemasVersion = "0.0.16.1"

    commonsIoVersion = "2.14.0"
    commonsCollectionsVersion = "4.4"
    cxfVersion = "3.6.2"
    cxfPluginVersion = "4.0.0"
    embeddedRedisVersion = "0.7.2"
    guavaVersion = "32.1.3-jre"
    jakartaJwsApiVersion = "3.0.0"
    javaxJwsApiVersion = "1.1"
    jaxb2Version = "0.13.1"
    jaxb2NamespacePrefixVersion = "2.0"
    jaxbImplVersion = "2.3.8"
    saxonVersion = "12.3"
    shedlockSpringVersion = "4.46.0"
    springSecuritySaml2CoreVersion = "1.0.3.RELEASE" //Higher versions require opensaml 2.6.6 (not available 2020-03-23)
    uapVersion = "1.5.3"
    xmlSecVersion = "4.0.0"

    springbootMavenBomVersion = "2.7.17"
}

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven-publish'
    apply plugin: 'java'

    group = 'se.inera.intyg.infra'
    version = System.properties['buildVersion'] ?: '3.20.0-SNAPSHOT'

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories.maven {
            url = uri("https://nexus.drift.inera.se/repository/maven-releases/")
            credentials {
                username = System.properties['ineraNexusUsername']
                password = System.properties['ineraNexusPassword']
            }
        }
    }

    tasks.register('install') {
        description("Publishes artifact to local maven repository.")
        dependsOn(publishToMavenLocal)
    }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }
}

subprojects {
    apply plugin: 'se.inera.intyg.plugin.common'

    dependencies {
        implementation "javax.annotation:javax.annotation-api"
        implementation "javax.jws:javax.jws-api:${javaxJwsApiVersion}"
        implementation "jakarta.xml.bind:jakarta.xml.bind-api"
        implementation "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"
        implementation "jakarta.xml.ws:jakarta.xml.ws-api"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testImplementation "com.sun.xml.messaging.saaj:saaj-impl"
        testImplementation "junit:junit"
        testImplementation "org.glassfish.jaxb:jaxb-runtime"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-core"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.springframework:spring-test"

        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    }

    test {
        useJUnitPlatform()
    }

    tasks.register('tagRelease', TagReleaseTask)
    tasks.register('createVersionPropertyFile', VersionPropertyFileTask)

    compileTestJava.dependsOn('createVersionPropertyFile')
    checkstyleMain.dependsOn('createVersionPropertyFile')

    clean {
        setDelete(fileTree(project.layout.buildDirectory.get().asFile) {
            exclude("/tmp/.cache/expanded/expanded.lock")
        })
    }

    if (project.hasProperty('codeQuality')) {
        dependencies { spotbugsSlf4j "org.slf4j:slf4j-simple" }

        afterEvaluate {
            if (project.name == "hsatk-integration") {
                licenseMain.dependsOn(wsdl2javaTask)
            }
        }
    }
}
