plugins {
    id "se.inera.intyg.plugin.common" version "3.1.4" apply false
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'idea'
}


ext {
    funktionstjansterGrpSchemasVersion = "1.0.6"

    rivtaDirectoryOrganizationSchemasVersion = "1.1.RC5.6"
    rivtaStoreLogSchemasVersion = "2.0.1"
    rivtaHsaAuthorizationmanagementSchemasVersion = "1.0.RC4.6"
    rivtaHsaEmployeeSchemasVersion = "1.0.RC4.6"
    rivtaGetPersonsPersonSchemasVersion = "3.1.1"

    intygClinicalprocessSchemasVersion = "1.0.24"

    srsSchemasVersion = "0.0.16"

    schemasContractVersion = "2.1.8"

    aspectjWeaverVersion = "1.8.9"
    commonsIoVersion = "2.6"
    commonsCollectionsVersion = "4.4"
    cxfVersion = "3.2.4"
    embeddedRedisVersion = "0.7.2"
    guavaVersion = "28.2-jre"
    jakartaJwsApiVersion = "2.1.0"
    kryoVersion = "4.0.2"
    prometheusClientVersion = "0.8.1"
    saxonVersion = "9.9.1-6"
    shedlockSpringVersion = "4.5.0"
    springSecuritySaml2CoreVersion = "1.0.3.RELEASE" //Higher versions require opensaml 2.6.6 (not available 2020-03-23)
    uapVersion = "1.5.2"
    xmlSecVersion = "2.1.4"

    springbootMavenBomVersion = "2.2.5.RELEASE"

}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    dependencies {
        compile "javax.annotation:javax.annotation-api"
        compile "jakarta.xml.bind:jakarta.xml.bind-api"
        compile "jakarta.xml.ws:jakarta.xml.ws-api"
        compile "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testCompile "org.glassfish.jaxb:jaxb-runtime"
        testCompile "com.sun.xml.messaging.saaj:saaj-impl"

        testCompile "junit:junit:4.+"
        testCompile "org.springframework:spring-test"
        testCompile "org.mockito:mockito-core"
    }

    group = 'se.inera.intyg.infra'
    version = System.properties['buildVersion'] ?: '3.16.2-SNAPSHOT'

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 11
    targetCompatibility = 11

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }
}

task tagRelease(type: TagReleaseTask)
