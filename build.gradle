ext {
    rivtaClinicalprocessSchemasVersion = "2.0.RC6.2"
    rivtaEhrLogstoreSchemasVersion = "1.2.4"
    rivtaInsuranceprocessSchemasVersion = "3.1.0.4"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.4"
    rivtaHsaAuthorizationmanagementSchemasVersion = "1.0.RC4.4"
    rivtaHsaEmployeeSchemasVersion = "1.0.RC4.4"
    rivtaHsaOrganizationSchemasVersion = "1.1.RC5.4"
    intygInsuranceprocessSchemasVersion = "1.0.4"
    intygTransportstyrelsenSchemasVersion = "1.0.4"

    jacksonVersion = "2.8.1"
    jacksonDataformatVersion = "2.4.5"
    springVersion = "4.2.2.RELEASE"
    springSecurityVersion = "4.0.3.RELEASE"
    springSecurityExtensionsVersion = "1.0.1.RELEASE"
    cxfVersion = "3.1.3"
    slf4jVersion = "1.7.21"
    guavaVersion = "14.0.1"
    logbackVersion = "1.0.11"
    servletApiVersion = "2.5"
    commonsLangVersion = "3.1"
    commonsCodecVersion = "1.10"
    saxonVersion = "9.6.0-4"
    activeMqClientVersion = "5.13.0"
    jmsApiVersion = "1.1-rev-1"
    groovyVersion = "2.4.7"
    httpBuilderVersion = "0.7.1"
    fitnesseVersion = "20150814"
    gebCoreVersion = "0.10.0"
    seleniumServerVersion = "2.52.0"
    jsonAssertVersion = "1.2.0"
    commonsDbcp2Version = "2.0"

    mockitoVersion = "1.9.0"
    servletApiTestVersion = "3.1.0"
}

allprojects {
    group = 'se.inera.intyg.common'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    apply plugin: 'inera-intyg'
    apply plugin: 'maven'
    apply plugin: 'org.ajoberstar.grgit'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    apply plugin: 'checkstyle'

    checkstyle {
        config = resources.text.fromFile("$rootProject.projectDir/build-tools/src/main/resources/checkstyle/checkstyle.xml")
        ignoreFailures = false
        showViolations = true
    }

    checkstyleMain.onlyIf { project.hasProperty('codeQuality') }
    checkstyleMain.source = "src/main/java" // Explicitly disable generated code
    checkstyleTest.enabled = false

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.7.6.201602180812"
    }

    test {
        jacoco {
            destinationFile = file("$buildDir/jacoco/test.exec")
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases") {
                    authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
                }
            }
        }
    }

    dependencies {
        testCompile "junit:junit:4.+"
        testCompile "org.springframework:spring-test:${springVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }
}

task tagRelease(type: TagReleaseTask)
                      
task testReport(type: SharedTestReportTask)

apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.jacoco.reportPath", "$buildDir/jacoco/test.exec"
        property "sonar.projectName", "Intyg Common"
        property "sonar.projectKey", "intyg-common"
        property "sonar.host.url", System.properties['sonarUrl'] ?: "https://build-inera.nordicmedtest.se/sonar"
        property "sonar.test.exclusions", "src/test/**"
        property "sonar.exclusions", ["**/stub/**", "**/exception/**", "**/*Exception*.java", "**/templates.js", "**/vendor/**"]
        property "sonar.javascript.lcov.reportPath", "build/karma/merged_lcov.info"
    }
}

buildscript {
    dependencies {
        classpath 'se.inera.intyg:gradle-intyg-plugin:1.0.+'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.+"
        classpath 'org.ajoberstar:gradle-git:1.+'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.+'
        classpath 'com.moowork.gradle:gradle-grunt-plugin:0.+'
    }

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}
