plugins {
    id "io.spring.dependency-management" version "1.1.6" apply false
    id 'test-report-aggregation'
    id 'idea'
    id 'org.sonarqube' version '5.1.0.4882'
    id "org.owasp.dependencycheck" version "10.0.4"
    id 'jacoco'
}

ext {
    funktionstjansterGrpSchemasVersion = "2.0.0.2"
    intygClinicalprocessSchemasVersion = "1.0.24.2"
    rivtaGetPersonsPersonSchemasVersion = "3.1.1.2"
    rivtaStoreLogSchemasVersion = "2.0.1.2"
    schemasContractVersion = "2.1.8.2"
    schemasSupportVersion = "2.1.5.2"
    srsSchemasVersion = "0.0.16.2"

    commonsIoVersion = "2.14.0"
    commonsCollectionsVersion = "4.4"
    cxfVersion = "4.0.5"
    cxfPluginVersion = "4.0.0"
    guavaVersion = "32.1.3-jre"
    jakartaJwsApiVersion = "3.0.0"
    jaxb2Version = "3.0.0"
    jaxb2NamespacePrefixVersion = "2.0"
    saxonVersion = "12.3"
    shedlockSpringVersion = "4.46.0"
    uapVersion = "1.6.1"
    xmlSecVersion = "4.0.0"
    xmlWsRtVersion = "4.0.2"

    springbootMavenBomVersion = "3.3.4"

    nvdMirror = 'https://vulnz-nvd-mirror-devtest.intyg.nordicmedtest.se'
}

allprojects {
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'se.inera.intyg.infra'
    version = System.properties['buildVersion'] ?: '4.0.0-SNAPSHOT'
    sourceCompatibility = 21
    targetCompatibility = 21

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springbootMavenBomVersion}"
        }
    }

    dependencies {
        implementation "ch.qos.logback:logback-classic"
        implementation "jakarta.annotation:jakarta.annotation-api"
        implementation "jakarta.jws:jakarta.jws-api:${jakartaJwsApiVersion}"
        implementation "jakarta.xml.ws:jakarta.xml.ws-api"

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation "junit:junit"
        testImplementation "org.glassfish.jaxb:jaxb-runtime"
        testImplementation "org.junit.jupiter:junit-jupiter-api"
        testImplementation "org.junit.jupiter:junit-jupiter-params"
        testImplementation "org.mockito:mockito-core"
        testImplementation "org.mockito:mockito-junit-jupiter"
        testImplementation "org.springframework:spring-test"

        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
    }

    tasks.register("install") {
        description("Publishes artifact to local maven repository.")
        dependsOn(publishToMavenLocal)
    }

    compileJava {
        compileJava.options.encoding = 'UTF-8'
        options.compilerArgs.add("-parameters")
    }

    jar.duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    test { useJUnitPlatform() }

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories.maven {
            url = uri("https://nexus.drift.inera.se/repository/maven-releases/")
            credentials {
                username = System.properties['ineraNexusUsername']
                password = System.properties['ineraNexusPassword']
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }
}

subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
    dependencies { testReportAggregation project(":${subprojectName}") }
}


sonar {
    properties {
        property "sonar.projectKey", "intyg-infra"
        property "sonar.projectName", "intyg-infra"
        property "sonar.dependencyCheck.jsonReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.json" as String
        property "sonar.dependencyCheck.htmlReportPath", "${layout.buildDirectory.get()}/reports/dependency-check-report.html" as String
    }
}

dependencyCheck {
    formats = List.of("HTML", "JSON")
    nvd.setDatafeedUrl(nvdMirror)
    analyzers.setAssemblyEnabled(false)
}

reporting {
    dependencies {
        testReportAggregation project(':common-redis-cache-core')
        testReportAggregation project(':dynamiclink')
        testReportAggregation project(':grp-stub')
        testReportAggregation project(':ia-integration')
        testReportAggregation project(':pu-integration')
        testReportAggregation project(':srs-integration')
        testReportAggregation project(':postnummerservice-integration')
        testReportAggregation project(':log-messages')
        testReportAggregation project(':loggtjanst-stub')
        testReportAggregation project(':security-common')
        testReportAggregation project(':security-authorities')
        testReportAggregation project(':security-filter')
        testReportAggregation project(':security-siths')
        testReportAggregation project(':sjukfall-engine')
        testReportAggregation project(':xmldsig')
        testReportAggregation project(':monitoring')
        testReportAggregation project(':intyginfo')
        testReportAggregation project(':integreradeenheter')
        testReportAggregation project(':driftbanner-dto')
        testReportAggregation project(':testcertificate')
        testReportAggregation project(':message')
        testReportAggregation project(':privatepractitioner')
        testReportAggregation project(':certificate')
        testReportAggregation project(':hsa-integration-api')
        testReportAggregation project(':hsa-integration-intyg-proxy-service')
    }
}